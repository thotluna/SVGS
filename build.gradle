buildscript {
    ext {
        activity_version = '1.6.0-alpha01'
        compose_version = '1.1.1'
        coroutinesVersion = '1.5.2'
        firebase_version = '29.2.1'
        fundation_version = '1.2.0-alpha06'
        google_services_version = '4.3.10'
        hilt_version = '2.40'
        hilt_navigation_version = '1.0.0'
        hilt_viewmodel_version = '1.0.0-alpha03'
        hilt_test_version = '2.28-alpha'
        lifecycle_version = '2.5.0-alpha05'
        mockk_version = "1.12.0"
        navigation_version = '2.4.1'
        ui_version = '1.2.0-alpha06'
        androidXTestVersion = '1.4.0'
        splash_version = '1.0.0-beta02'
    }
    dependencies {
        // other plugins...
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "com.google.gms:google-services:$google_services_version"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.10' apply false
}

configurations {
    ktlint
}



dependencies {
    ktlint("com.pinterest:ktlint:0.44.0") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task ktlintFormatF(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "src/**/*.kt"
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

tasks.getByPath(':app:preBuild').dependsOn installGitHook